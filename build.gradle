import groovy.json.JsonSlurper

apply plugin: 'application'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

group 'org.iot.dsa'
mainClassName = 'org.iot.dsa.dslink.DSLink'
sourceCompatibility = 1.8
targetCompatibility = 1.8

build {
    def inputFile = file('dslink.json')
    def json = new JsonSlurper().parseText(inputFile.text)
    version = json.version
}

repositories {
    maven {
        url 'https://repo.sdc-automation.se/releases'
        credentials {
            username = System.getenv("SDC_REPO_USER")
            password = System.getenv("SDC_REPO_USER_TOKEN")
        }
    }
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    //implementation 'org.iot-dsa:dslink-v2-websocket:+' //for a locally installed sdk
//    implementation 'com.github.iot-dsa-v2.sdk-dslink-java-v2:dslink-v2-websocket:+'
    implementation 'com.github.iot-dsa-v2.sdk-dslink-java-v2:dslink-v2-websocket:0.79.2'
    //implementation 'com.github.iot-dsa-v2:sdk-dslink-java-v2:+'
    testImplementation group: 'org.python', name: 'jython-standalone', version: '2.7.0'
    testImplementation 'com.github.bkromhout:java-diff-utils:+'
    testImplementation 'org.apache.commons:commons-lang3:3.4'
    testImplementation 'junit:junit:+'
}

applicationDistribution.from(new File(project.projectDir, "/dslink.json"))

run {
    args System.getProperty("exec.args", "").split()
    workingDir project.buildDir
}

task sourcesJar(group: 'build', type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allJava+sourceSets.test.allJava
}

task testJar(group: 'build', type: Jar, dependsOn: classes) {
    classifier = 'tests'
    from sourceSets.test.output
}

artifacts {
    archives sourcesJar
    archives testJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'com.github.iot-dsa-v2'
            artifactId = 'driver-framework-java-v2-prototype'
            version = '1.1.5'

            artifact sourcesJar
            artifact testJar
        }
    }

    repositories {
        maven {
            url 'https://repo.sdc-automation.se/releases'
            credentials {
                username = System.getenv("SDC_REPO_USER")
                password = System.getenv("SDC_REPO_USER_TOKEN")
            }
        }
    }
}

wrapper {
    gradleVersion = '4.10.2'
}
